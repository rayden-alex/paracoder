import java.text.SimpleDateFormat

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.2'
	id 'io.spring.dependency-management' version '1.1.4'
//	id 'org.graalvm.buildtools.native' version '0.9.28'
}

group = 'by.rayden'
version = '1.0.3'

java {
	sourceCompatibility = '21'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}

	byteBuddyAgent
}

configurations.configureEach {
//	https://gradlehero.com/how-to-exclude-gradle-dependencies/
	exclude group: 'io.micrometer'
}

repositories {
	mavenCentral()
}

dependencies {
	//to get rid of "unknown enum constant javax.annotation.meta.When.MAYBE" warning
    //compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
	compileOnly 'com.github.spotbugs:spotbugs-annotations:4.8.0'
	compileOnly 'org.jetbrains:annotations:24.0.1'

	implementation 'net.java.dev.jna:jna:5.13.0'

	implementation 'info.picocli:picocli-spring-boot-starter:4.7.5'
	implementation 'info.picocli:picocli-jansi-graalvm:1.2.0'
	implementation 'commons-io:commons-io:2.11.0'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'info.picocli:picocli-codegen:4.7.5'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	byteBuddyAgent "net.bytebuddy:byte-buddy-agent"
}

tasks.withType(JavaCompile).configureEach {
	options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]

	//Enable JDK21 preview features, see https://docs.gradle.org/current/userguide/building_java_projects.html#sec:feature_preview
	options.compilerArgs += "--enable-preview"

	// https://gist.github.com/rponte/d660919434d094bbd35a1aabf7ef1bf0
	// Although Spring Boot Gradle Plugin configures any JavaCompile tasks with no configured encoding to use UTF-8.
	// https://docs.spring.io/spring-boot/docs/3.2.2/gradle-plugin/reference/htmlsingle/#reacting-to-other-plugins.java
	options.encoding = "UTF-8"
}

tasks.withType(Test).configureEach {
	jvmArgs += "--enable-preview"

	// To get rid of "WARNING: A Java agent has been loaded dynamically ... byte-buddy-agent..."
	// https://github.com/raphw/byte-buddy/discussions/1535
	jvmArgs += "-javaagent:${configurations.byteBuddyAgent.singleFile}"
	defaultCharacterEncoding = "UTF-8"
}

tasks.withType(JavaExec).configureEach {
	jvmArgs += "--enable-preview"
}

tasks.withType(Javadoc).configureEach {
	options.encoding = "UTF-8"
}

def generatedResources = "$buildDir/generated-resources/main"
sourceSets {
	main {
		//Register an output folder on the main SourceSet:
		output.dir(generatedResources, builtBy: 'generateVersionTxt')
		//It is now a part of the 'main' classpath and will be a part of the jar
	}
}

//A task that generates the "version.txt" resources:
tasks.register('generateVersionTxt') {
	description 'Creates a version.txt file with build info that is added to the root of the jar'
	doLast {
		new File(generatedResources).mkdirs()
		def generated = new File(generatedResources, "version.txt")
		generated.text = """
Version: $rootProject.version
Buildtime: ${new SimpleDateFormat("yyyy-MM-dd HH:mm:ss").format(new Date())}
Application-name: $rootProject.name
"""
	}
}

test {
	useJUnitPlatform()
}
